import org.apache.tools.ant.filters.ReplaceTokens;

// didn't figure out how to use neither javax.sql.DataSource nor javax.sql.XADataSource => exclude those tests
unitTest.useJUnit {
      excludeCategories 'com.p6spy.engine.spy.DataSourceTests', 'com.p6spy.engine.spy.XADataSourceTests'
}

String rootDir = "$buildDir/unpack/apache-tomcat"
String binDir = rootDir + "/bin"

task deployP6SpyJars(type: Copy, dependsOn: unpackContainer) {
  from configurations.p6spyAll
  into rootDir + "/lib"
}

task copyConfig(type: Copy, dependsOn: unpackContainer) {
  from "$buildDir/../src/test/config/spy_with_explicit_drivername.properties"
  into rootDir + "/lib"
  filter(ReplaceTokens, tokens: [logDir: buildDir.getAbsolutePath()])
  doLast {
    ant.rename(src: rootDir + '/lib/spy_with_explicit_drivername.properties', dest: rootDir + '/lib/spy.properties')
  }
}

task patchContainer(type: Copy, dependsOn: unpackContainer) {
  from "$buildDir/../src/test/container/tomcat-common"
  into rootDir
  filter(ReplaceTokens, tokens: [buildDir: buildDir.getAbsolutePath()])
}

task startContainer(type:Exec, dependsOn: [ deployP6SpyJars, copyConfig, patchContainer]) {
  doFirst {
    ant.chmod( dir: "$binDir", includes: "*.sh", perm: "+x")
  }
  workingDir binDir
  commandLine 'bash', '-e', 'startup.sh'
  
  // not for tomee
  if (!container.startsWith("tomee")) {
    environment CATALINA_OPTS: "-Djava.rmi.server.hostname:127.0.0.1 -Dcom.sun.management.jmxremote.port=8050 -Dcom.sun.management.jmxremote.ssl=false -Dcom.sun.management.jmxremote.authenticate=false"
  }
}

task stopContainer(type:Exec, dependsOn: startContainer) {
  workingDir binDir
  commandLine 'bash', '-e', 'shutdown.sh'
}
